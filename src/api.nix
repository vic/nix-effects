# nix-effects API layer
#
# Provides mk/extractValue/extractTests for structured module definitions.
# The mk { doc, value, tests } pattern is adapted from nfx by Victor Borja
# (https://github.com/vic/nfx), licensed under Apache-2.0.
{ lib }:

rec {
  # Wrap a value with documentation and inline tests.
  # Usage: mk { doc = "description"; value = <impl>; tests = { name = { expr; expected; }; }; }
  mk = { doc ? "", value, tests ? {} }: {
    _type = "nix-effects-api";
    inherit doc value tests;
  } // (lib.optionalAttrs (lib.isFunction value) { __functor = _self: value; });

  # Recursively extract raw values, stripping mk wrappers.
  extractValue = x:
    if x ? value && x._type or null == "nix-effects-api" then extractValue x.value
    else if builtins.isAttrs x && !(x ? _tag)
    then builtins.mapAttrs (_: extractValue) x
    else x;

  # Recursively extract all inline tests from nested mk wrappers.
  extractTests = x:
    let
      ownTests =
        if x ? _type && x._type == "nix-effects-api" && x.tests != {}
        then lib.mapAttrs' (name: test: {
          name = "test-${name}";
          value = test;
        }) x.tests
        else {};
      childTests =
        let
          isAPI = builtins.isAttrs (x.value or null) && x._type or null == "nix-effects-api";
          isRaw = builtins.isAttrs x && !(x ? _type) && !(x ? _tag);
        in
        if isAPI then lib.mapAttrs (_: extractTests) x.value
        else if isRaw then lib.mapAttrs (_: extractTests) x
        else {};
    in ownTests // childTests;

  # Recursively extract documentation from nested mk wrappers.
  # Returns hierarchical attrset: { doc, tests, fnName = { doc, tests }, subModule = { ... }, ... }
  # Unlike extractTests (flat, dotted prefixes), extractDocs preserves module hierarchy.
  # When a mk wrapper's value is itself an attrset of mk-wrapped functions (module pattern),
  # the inner docs are merged in alongside this node's own doc/tests.
  extractDocs = x:
    if x ? value && x._type or null == "nix-effects-api"
    then
      { inherit (x) doc tests; } //
      (if builtins.isAttrs x.value && !(x.value ? _tag)
       then lib.filterAttrs (_: v: v != {})
            (builtins.mapAttrs (_: extractDocs) x.value)
       else {})
    else if builtins.isAttrs x && !(x ? _tag)
    then lib.filterAttrs (_: v: v != {})
         (builtins.mapAttrs (_: extractDocs) x)
    else {};

  # Run collected tests, returning { allPass, passed, failed, summary }.
  # Handles nested namespaces from extractTests: recurses into attrsets
  # that lack `expr` (namespaces), runs leaf attrsets with `expr` + `expected`.
  runTests = tests:
    let
      # Flatten nested test tree into { "ns.sub.test-name" = { expr; expected; }; }
      flatten = prefix: attrs:
        lib.foldlAttrs (acc: name: value:
          let path = if prefix == "" then name else "${prefix}.${name}";
          in if builtins.isAttrs value && value ? expr && value ? expected
             then acc // { ${path} = value; }
             else if builtins.isAttrs value
             then acc // (flatten path value)
             else acc
        ) {} attrs;
      flat = flatten "" tests;
      results = builtins.mapAttrs (name: test:
        let actual = test.expr; in
        { inherit name actual; expected = test.expected; pass = actual == test.expected; }
      ) flat;
      passedNames = lib.filterAttrs (_: r: r.pass) results;
      failedNames = lib.filterAttrs (_: r: !r.pass) results;
      nPassed = builtins.length (builtins.attrNames passedNames);
      nFailed = builtins.length (builtins.attrNames failedNames);
    in {
      inherit results;
      passed = passedNames;
      failed = failedNames;
      allPass = nFailed == 0;
      summary = "${toString nPassed} passed, ${toString nFailed} failed";
    };
}
